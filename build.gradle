plugins {
    id "groovy"
    id "java"
    id 'jacoco'
    id "idea"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'kr.entree.spigradle' version '2.2.4'
    // TODO Switch to Lombok plugin instead of using the non-plugin version, and update Gradle (only can do after Ptero4J is no longer a dependency)
    //id "io.freefair.lombok" version "6.1.0-m3"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

group 'com.sparkedhost'
version '1.2.0-test09'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = uri('https://oss.sonatype.org/content/repositories/snapshots') }
    maven { url = 'https://jitpack.io' }
    maven {
        url = 'https://repo.mattmalec.com/repository/releases'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    allprojects {
        repositories {
            maven { url 'https://jitpack.io' }
        }
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:22.0.0'
    compileOnly spigot('1.12.2')
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.mattmalec:Pterodactyl4J:2.BETA_92'
    compileOnly 'me.clip:placeholderapi:2.11.1'
}

spigot {
    authors = ['BetTD']
    load = POST_WORLD
    softDepends = ['PlaceholderAPI']
    commands {
        perf {
            aliases = ['performance', 'lag', 'gc']
            description = 'Displays the server resource usage'
            permission = 'readings.perf'
        }
    }
    permissions {
        'readings.perf' {
            description = 'Gives access to resource usage commands'
            defaults = 'op'
        }
        'readings.restart' {
            description = 'Restarts the server using the Pterodactyl API'
            defaults = 'op'
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    dependencies {
        exclude dependency('org.jetbrains:annotations:22.0.0')
    }
}

tasks.build.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

defaultTasks 'build'
