plugins {
    id "groovy"
    id "java"
    id 'jacoco'
    id "idea"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'kr.entree.spigradle' version '2.2.4'
    // TODO Switch to Lombok plugin instead of using the non-plugin version, and update Gradle (only can do after Ptero4J is no longer a dependency)
    //id "io.freefair.lombok" version "6.1.0-m3"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

group 'com.sparkedhost'
version '1.2.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = uri('https://oss.sonatype.org/content/repositories/snapshots') }
    maven { url = 'https://jitpack.io' }
    allprojects {
        repositories {
            maven { url 'https://jitpack.io' }
        }
    }
}

dependencies {
    compileOnly spigot('1.12.2')
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    // TODO Remove Ptero4J and make our own wrapper
    compile 'com.github.stanjg:Ptero4J:master-SNAPSHOT'
    compile 'com.squareup.okio:okio:2.10.0'
    compile 'com.squareup.okhttp3:okhttp:4.9.1'
    compile 'org.json:json:20210307'
    testImplementation 'junit:junit:4.13.2'
}

spigot {
    authors = ['BetTD']
    load = POST_WORLD
    commands {
        perf {
            aliases = ['performance', 'lag', 'gc']
            description = 'Displays the server resource usage'
            permission = 'readings.perf'
        }
    }
    permissions {
        'readings.perf' {
            description = 'Gives access to resource usage commands'
            defaults = 'op'
        }
        'readings.restart' {
            description = 'Restarts the server using the Pterodactyl API'
            defaults = 'op'
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    dependencies {
        include(dependency('com.github.stanjg:Ptero4J:master-SNAPSHOT'))
        include(dependency('com.squareup.okhttp3:okhttp:4.9.1'))
        include(dependency('com.squareup.okio:okio:2.10.0'))
        include(dependency('org.json:json:20210307'))
    }
}

tasks.build.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

defaultTasks 'build'
